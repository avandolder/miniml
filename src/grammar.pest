WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ "#" ~ (!NEWLINE ~ ANY) ~ NEWLINE }

Program = { SOI ~ Term ~ EOI }
Term = { Apply | ValTerm | "(" ~ Term ~ ")" }
Apply = { ("(" ~ Term ~ ")" | Id) ~ ValTerm+ }
ValTerm = _{ Let | Match | Lambda | True | False | Id | "(" ~ ValTerm ~ ")"}
Lambda = { "fn" ~ Pattern ~ ":" ~ Type ~ "=>" ~ Term }
Let = { "let" ~ Pattern ~ ":" ~ Type ~ "=" ~ Term ~ "in" ~ Term }

Type = { Arrow | Bool | "(" ~ Type ~ ")" }
Arrow = { ("(" ~ Type ~ ")" | Bool) ~ "->" ~ Type }

Match = { "match" ~ Term ~ "with" ~ "|"? ~ Arm ~ ("|" ~ Arm)* ~ "end" }
Arm = { Pattern ~ "=>" ~ Term }
Pattern = { Any | True | False | Id }

Id = @{
    !("let" | "in" | "fn" | "true" | "false" | "match" | "with") ~
    ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_" | "'")*
}
Any = @{ "_" }
True = @{ "true" }
False = @{ "false" }
Bool = @{ "Bool" }
